class QuickSort
{
    function void swap (Array A, int i, int j)
    {
        var int temp;
        let temp = A[i];
        let A[i] = A[j];
        let A[j] = temp;
        return;
    }

    function int partition (Array A, int p, int r)
    {
        var int pivot, i, j;

        let pivot = A[r];
        let i = p - 1;
        let j = p;

        while (j < r)
        {
            if (A[j] < pivot)
            {
                let i = i + 1;
                do QuickSort.swap(A, i, j);
            }

            let j = j + 1;
        }

        do QuickSort.swap(A, i + 1, r);

        return (i + 1);
    }

    function void sort (Array A, int p, int r)
    {
        var int q;

        if (p < r)
        {
            let q = QuickSort.partition(A, p, r);

            do QuickSort.sort(A, p, q - 1);
            do QuickSort.sort(A, q + 1, r);
        }

        return;
    }

    function void print (Array A, int n)
    {
        var int i;

        while (i < n)
        {
            do Output.printInt(A[i]);
            do Output.printString(" ");
            let i = i + 1;
        }

        return;
    }
}